100 SCREEN 1,2,0:WIDTH32:COLOR15,1,1:KEYOFF
105 CLEAR200,&HC000:DEFUSR=&H7E:A=USR(0):BLOAD"PROGRAM.BIN":DEFUSR1=&HC000:A=USR1(0) ' CHARACTER DEFINE
110 DEFINTA-Z:V1$="abccdeecaa`":V2$="pqrstsuqusv":DIMEN(6):RD=1:SC=0:LF=0:ST=4:GOSUB1300 ' SCREEN MAKE
120 ' *** MAIN LOOP ***
130 ONST+1GOSUB150,200,700,800,900 '0:START/1:GAME/2:CLEAR/3:MISS/4:OVER
140 GOTO130
150 ' *** START ***
160 GOSUB1200 ' LCD CLEAR
162 V$=" "+RIGHT$(STR$(RD),2)+"-"+RIGHT$("     "+STR$(SC),6):FORI=0TO9:POKE&HC000+I,ASC(MID$(V$,I+1,1)):NEXT:GOSUB1100 ' LCD PRINT
164 W=3:GOSUB1000 ' WAIT
166 GOSUB1200 ' LCD CLEAR
170 FORI=0TO6:EN(I)=0:NEXT:NM=1:SF=0:EL=16:ES=0:SL=30:WC=999:UF=0:WT=30-RD*2:IFWT<10THENWT=10
180 ST=1:RETURN
200 ' *** MAIN ***
210 GOSUB300 'PLAYER
220 GOSUB500 'ENEMY
230 GOSUB600 'DISPLAY
240 RETURN
300 ' --- PLAYER ---
310 S=STICK(0)+STICK(1):IFS=0THENSF=0
320 IFS=7ANDSF=0THENSF=1:NM=NM+1:IFNM>11THENNM=1
330 IFS=3ANDSF=0THENSF=1:GOSUB400
340 RETURN
400 ' --- SHOT ---
410 F=0:IFSL>0THENSL=SL-1:FORI=0TO5:IFEN(I)=NMTHENF=I+1:EN(I)=0:I=5:NEXTELSENEXT
420 IFF=0THENPLAY"T255S12M15O2C16":RETURN ' NO HIT
430 PLAY"T255S12M3O5L32D":IFNM=11THENSC=SC+300ELSESC=SC+10
440 IFF>1THENFORI=F-1TO1STEP-1:EN(I)=EN(I-1):EN(I-1)=0:NEXT
450 F=0:FORI=0TO5:F=F+EN(I):NEXT:IFEL=0ANDF=0THENST=2:RETURN 'CLEAR
460 IFNM>1ANDNM<11THENES=ES+NM-1:IFESMOD10=0THENUF=1 ' UFO CHECK
470 RETURN
500 ' --- ENEMY ---
510 WC=WC+1:IFWC<WTTHENRETURN
520 IFEN(0)>0THENST=3:RETURN 'MISS
530 FORI=0TO4:EN(I)=EN(I+1):NEXT:EN(5)=0
540 IFEL>0THENEL=EL-1:IFUF=1THENUF=0:EN(5)=11ELSEEN(5)=INT(RND(1)*10)+1
550 WC=0:RETURN
600 ' *** DISPLAY ***
610 POKE&HC000,32:POKE&HC001,NM:POKE&HC002,LF+64
620 FORI=0TO5:IFEN(I)>0THENPOKE&HC003+I,EN(I)ELSEPOKE&HC003+I,32
630 NEXT:GOSUB1100 ' LCD PRINT
640 RETURN
700 ' *** CLEAR ***
710 RD=RD+1:PLAY"T255S12M2O5L8ERERERERRR"
720 IFPLAY(0)>0THEN720ELSEST=0:RETURN
800 ' *** MISS ****
810 PLAY"T255S12M3O4L2D":W=3:GOSUB1000 ' WAIT
820 LF=LF-1:IFLF<=0THENST=4:RETURN
830 V$="    "+RIGHT$("     "+STR$(SC),6):FORI=0TO9:POKE&HC000+I,ASC(MID$(V$,I+1,1)):NEXT:GOSUB1100 ' LCD PRINT
840 W=3:GOSUB1000 ' WAIT
860 GOSUB1200 ' LCD CLEAR
870 FORI=0TO6:IFEN(I)>0THENEL=EL+1
880 EN(I)=0:NEXT:NM=1:SF=0:ES=0:WC=999:ST=1:RETURN
900 ' *** OVER ***
902 V$=" "+RIGHT$(STR$(RD),2)+"-"+RIGHT$("     "+STR$(SC),6):FORI=0TO9:POKE&HC000+I,ASC(MID$(V$,I+1,1)):NEXT:GOSUB1100 ' LCD PRINT
905 LOCATE11,6:PRINT"GAME  OVER";:LOCATE11,15:PRINT"PUSH SPACE";
910 IFSTRIG(0)+STRIG(1)=0THEN910
920 LOCATE11,6:PRINT"          ";:LOCATE11,15:PRINT"          ";:GOSUB1200 ' LCD CLEAR
930 R=RND(-TIME):RD=1:SC=0:LF=3:ST=0:RETURN
1000 ' *** WAIT ***
1010 W=W*60:TIME=0
1020 IFTIME<WTHEN1020ELSERETURN
1100 ' *** LCD PRINT ***
1110 FORI=0TO10:P=PEEK(&HC000+I)
1120 IFP=32THENLOCATE11+I,10:PRINT"`";:LOCATE11+I,11:PRINT"`";:GOTO1170
1130 IFP=45THENLOCATE11+I,10:PRINT"`";:LOCATE11+I,11:PRINT"y";:GOTO1170
1140 IFP>64THENLOCATE11+I,10:PRINTMID$("``f",P-64,1);:LOCATE11+I,11:PRINTMID$("xww",P-64,1);:GOTO1170
1150 IFP>47ANDP<58THENP=P-47
1160 LOCATE11+I,10:PRINTMID$(V1$,P,1);:LOCATE11+I,11:PRINTMID$(V2$,P,1);
1170 NEXT:RETURN
1200 ' *** LCD CLEAR ***
1210 FORI=0TO9:POKE&HC000+I,32:LOCATE11+I,10:PRINT"`";:LOCATE11+I,11:PRINT"`";:NEXT:RETURN
1300 ' *** SCREEN MAKE ***
1310 FORI=0TO5:FORJ=0TO11:READA:LOCATE10+J,8+I:PRINTCHR$(A);:NEXT:NEXT
1320 READA,B,C,D:LOCATE10,19:PRINTCHR$(A);CHR$(B);:LOCATE10,20:PRINTCHR$(C);CHR$(D);"AIM"
1330 READA,B,C,D:LOCATE16,19:PRINTCHR$(A);CHR$(B);:LOCATE16,20:PRINTCHR$(C);CHR$(D);"FIRE"
1340 RETURN
1400 DATA136,137,138,139,140,141,142,143,144,144,144,144
1410 DATA128,133,133,133,133,133,133,133,133,133,133,129
1420 DATA134, 32, 32, 32, 32, 32, 32, 32, 32, 32, 32,135
1430 DATA134, 32, 32, 32, 32, 32, 32, 32, 32, 32, 32,135
1440 DATA130,132,132,132,132,132,132,132,132,132,132,131
1450 DATA144,144,144,144,144,144,144,144,144,144,144,144
1460 DATA145,146,147,148 : ' ALLOW LEFT
1470 DATA149,150,151,152 : ' ALLOW RIGHT
